robot "SawBot"

// SawBot: Heads to the center of the arena, then picks a random axis
// and oscillates back and forth along it while scanning.
//
// Debug values: phase | spd

var axis angle = angle(0)
var spd float = 60.0
var tickCount int = 0
var legTicks int = 120
var phase int = 0
var targetX float = arenaWidth() * 0.5 + float(random(10))
var targetY float = arenaHeight() * 0.5 + float(random(10))
var turnRate float = 6.0
var scanBackRate float = -(turnRate / 3.0)
var scanBackTicks float = 15.0
var scanBackCounter float = 0.0


func init() {
	setColor(200, 80, 40)
	setGunColor(120, 120, 120)
	setRadarColor(255, 200, 0)

	legTicks = 100
	axis = angle(random(360))
}

func tick() {
    bearingToTarget := bearingTo(targetX, targetY)
    dist := distanceTo(targetX, targetY)

	if phase == 0 {
		// Head to center — bearingTo is relative, convert to absolute for setHeading
		setHeading(getHeading() + bearingToTarget)
		spd = 60.0

		if dist < 30.0 {
			// Arrived at center — switch to oscillation
			phase = 1
		}
	}

	if phase == 1 {
        setHeading(axis)
		if dist > 25.0 {
			// bearingTo is relative to heading (≈axis). Near 0 = ahead, ±180 = behind.
            if abs(float(bearingToTarget)) < 90.0 {
              spd = 60.0
            } else {
              spd = -60.0
            }
		}
	}

	setSpeed(spd)
    // Wait one tick to scan back
    if scanBackCounter > 0.0 && scanBackCounter != scanBackTicks {
      setRadarTurnRate(scanBackRate)
      setGunHeading(getRadarHeading() + angle(scanBackRate))
    } else {
      setRadarTurnRate(turnRate)
      setGunHeading(getRadarHeading() + angle(turnRate))
    }

	// Emit debug values: phase, spd
	debugInt(phase)
	debugFloat(spd)
    debugAngle(bearingToTarget)
    debugAngle(bearingToTarget - axis)
    debugFloat(dist)
}

on wallHit(bearing angle) {
	if phase == 0 {
		// Shouldn't hit a wall going to center, but handle it
		phase = 1
		setHeading(axis)
		spd = 50.0
	} else {
		spd = spd * -1.0
		tickCount = 0
	}
}

on scan(distTo float, bearing angle) {
    if scanBackCounter == 0.0 {
      scanBackCounter = scanBackTicks
    } else {
      scanBackCounter -= 1.0
    }

    gunRadarDiff := getGunHeading() - getRadarHeading()
    if (getGunHeat() == 0.0 && gunRadarDiff < angle(5.0)) {
      fire(4.0)
    }
}
